grammar edu.upb.compilacion.ListaCompiler with org.eclipse.xtext.common.Terminals

generate listaCompiler "http://www.upb.edu/compilacion/ListaCompiler"

Lista:
	(definitions+=FunctionDefinition | evaluations+=Evaluation)+;

Evaluation:
	"?" return=Expression ";"
;

FunctionDefinition: name=IDFUNCVAR "(" (params+=MyVariable (',' params+=MyVariable)*)? ")"
	"=" return=Expression ";"
;

Expression: 
	exp=FirstLevelExp
;

FirstLevelExp:
	args+=SecondLevelExp (op=FirstLevelOp args+=FirstLevelExp)?
;

SecondLevelExp:
	args+=ThirdLevelExp (op=SecondLevelOp args+=SecondLevelExp)?
;

ThirdLevelExp:
	args+=FourthLevelExp (op=ThirdLevelOp args+=ThirdLevelExp)?
;

FourthLevelExp:
	args+=Term (op=FourthLevelOp args+=FourthLevelExp)?
;

Term:
	SimpleTerm | ComplexTerm
;

SimpleTerm:
	MyInteger | MyString | MyBool | Variable | List
;

ComplexTerm:
	FunctionCall | IfControlFlow | BracketExpression
;

BracketExpression:
	"(" exp=Expression ")"
;

MyInteger:
	PosInteger | NegInteger
;

PosInteger:
	val=INT
;

NegInteger:
	"-" val=(PosInteger | ComplexTerm)
;

MyVariable:
	Variable | CastedVariable
;

Variable:
	var=IDFUNCVAR
;

CastedVariable:
	var=IDFUNCVAR ":" type=CastedType
;

MyBool:
	PosBool | NegBool
;

PosBool:
	val=Bool
;

NegBool:
	"!" val=(PosBool | ComplexTerm)
;

MyString:
	val=STRING
;

IfControlFlow:
	"if" "(" cond=Expression "," iftrue=Expression "," iffalse=Expression ")"
;

FunctionCall:
	PreDefFunctionCall | UserDefFunctionCall
;

PreDefFunctionCall:
	function=PDFunction "(" (args+=Expression ("," args+=Expression)*)? ")"
;

UserDefFunctionCall:
	function=[FunctionDefinition|IDFUNCVAR] "(" (args+=Expression ("," args+=Expression)*)? ")"
;


List: 
	"["{List} (elems+=ListElem + ("," elems+=ListElem)*)?"]"
;

ListElem:
	MyInteger | Variable
;

enum Bool:
	true = "true" | false="false"
;

enum PDFunction:
	length = "length" |
	cons = "cons" |
	car = "car" |
	cdr = "cdr" |
	isEmpty = "isEmpty" |
	show = "show"
;

enum FirstLevelOp:
	and = "&" |
	or = "|"
;

enum SecondLevelOp:
	gt = ">" |
	lt = "<" |
	eq = "=="
;

enum ThirdLevelOp:
	plus = "+" |
	minus = "-" |
	concat = "++"
;

enum FourthLevelOp:
	mult = "*" |
	div = "/"
;

enum CastedType:
	int = "int" |
	bool = "bool" |
	string = "string" |
	list = "[int]"
;

terminal IDFUNCVAR: ('a'..'z'|'A'..'Z')('A'..'Z'|'a'..'z'|'0'..'9')*;